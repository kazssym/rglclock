dnl Process this file with autoconf to produce a configure script.
AC_INIT(glclock.cc)
dnl $Format: "AM_INIT_AUTOMAKE($Package$, $Version$)" $
AM_INIT_AUTOMAKE(rglclock, 1.2.2)
AM_CONFIG_HEADER(config.h)
AC_ARG_ENABLE(wm-command,
[  --disable-wm-command    do not set WM_COMMAND for the main window])
if test "$enable_wm_command" != no; then
  AC_DEFINE(SESSION)
fi

AC_ARG_ENABLE(wm-transient-for,
[  --disable-wm-transient-for
                          do not set WM_TRANSIENT_FOR for the dialog boxes])
if test "$enable_wm_transient_for" != no; then
  AC_DEFINE(ENABLE_TRANSIENT_FOR_HINT)
fi

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_PROG_RANLIB
dnl Checks for libraries.
AM_PATH_GTK
AC_SUBST(GL_LIBS)dnl
AC_CHECK_LIB(GL, glClear, GL_LIBS="-lGL $GL_LIBS",, $GL_LIBS $GTK_LIBS)
if test $ac_cv_lib_GL_glClear = no; then
  AC_CHECK_LIB(MesaGL, glClear, GL_LIBS="-lMesaGL $GL_LIBS",, $GL_LIBS $GTK_LIBS)
fi

AC_CHECK_LIB(GLU, gluLookAt, GL_LIBS="-lGLU $GL_LIBS",, $GL_LIBS $GTK_LIBS)
if test $ac_cv_lib_GLU_gluLookAt = no; then
  AC_CHECK_LIB(MesaGLU, gluLookAt, GL_LIBS="-lMesaGLU $GL_LIBS",, $GL_LIBS $GTK_LIBS)
fi

dnl Checks for header files.
AC_CHECK_HEADERS(getopt.h sys/time.h unistd.h)
dnl Checks for typedefs, structures, and compiler characteristics.
AC_DEFINE(_GNU_SOURCE)
AC_STRUCT_TM
AC_HEADER_TIME
AC_C_CONST
dnl Checks for library functions.
AC_CHECK_FUNCS(asprintf getopt_long gettimeofday)
AC_OUTPUT(Makefile)
